FUNCTION Main() {

	//Get tornitura as a list
	STRING LIST $TORNDATA = {}
	CALL GetTornitura($TORNDATA)
	
	//Define Workplanes
	STRING $WP_POZZETO = "PL POZZETTO"
	CALL GetWorkplanes($WP_POZZETO)
	
	//Call Procedure for Centrino
	CALL CSMD4($TORNDATA , $WP_POZZETO)
	
	
}

//Read tornitura file as a String List
FUNCTION GetTornitura(OUTPUT STRING LIST $TORNDATA) {

	//Check if the TORNITURA variable in not clear and have 5 characters,
	//then read tornio file and save it on a list $TORNDATA
	IF $TORNITURA != '' AND $TORNITURA != "NULL" {
		IF LENGTH($TORNITURA) != 5 {
			MESSAGE ERROR "Tornitura is wrong or can't be found!" + CRLF + "System have read this: " + $TORNITURA
			RETURN
		}
	}
	//Check which directory is available to read tornitura file
	STRING $DIRECTORY = ""
	STRING $DIRECTORYMATREX = "X:\cad\cam\tornio\"
	STRING $DIRECTORYALUMAT = "W:\cad\cam\tornio\"
	BOOL $DIRECTORYMATREXEXIST = DIR_EXISTS($DIRECTORYMATREX)
	BOOL $DIRECTORYALUMATEXIST = DIR_EXISTS($DIRECTORYALUMAT)
	
	IF $DIRECTORYMATREXEXIST {
		$DIRECTORY = $DIRECTORYMATREX
	} ELSEIF $DIRECTORYALUMATEXIST {
		$DIRECTORY = $DIRECTORYALUMAT
	} ELSE {
		MESSAGE ERROR "There's no directory available to read tornitura file"
		MESSAGE INFO "Macro aborted"
		MACRO ABORT
	}
	
	//Read tornitura file
	STRING $OPENFILE = "FILE OPEN " + $DIRECTORY + $TORNITURA + ".txt FOR READ AS TXTFILE"
	DOCOMMAND $OPENFILE
	FILE READ $TORNDATA FROM TXTFILE
	FILE CLOSE TXTFILE

}

///Define the workplane
FUNCTION GetWorkplanes(OUTPUT STRING $WP_POZZETO) {
	
	//Workplane for Pozzeto Side
	IF NOT ENTITY_EXISTS('Workplane', "PL POZZETTO") {
		MESSAGE INFO "Workplane <PL POZZETO> does not exist" + CRLF + "Select a Workplane from the next list"
		$WP_POZZETO = INPUT ENTITY WORKPLANE "Select Workplane for Pozzetto Holes:"
	}
	
	DEACTIVATE Workplane
	DEACTIVATE Featureset
	DIALOGS MESSAGE ON
	DIALOGS ERROR ON
}

//Female Pozzeto Feature Creation
FUNCTION CSMD4(STRING LIST $TORNDATA , STRING $WP_POZZETO) {
	
	REAL $DIAMETER = SUBSTRING($TORNDATA[12] , 5 , 5)
	REAL $HEIGHT = SUBSTRING($TORNDATA[13] , 5 , 5)
	//Import Centrino Feature Set
	DEACTIVATE Workplane
	ACTIVATE FOLDER "Featureset\STANDARD_HOLES\POZZETO"
	EDIT FEATURECREATE NAME   
	EDIT FEATURESET ; INSERT FILE FILEOPEN "V:\Master Files\Alumat\Powermill\Imports\Centering Holes\Three Holes\CENTERING_HOLES_3H.xml"
	DELETE Workplane "1"
	RENAME Featureset ; "CENTERING_HOLES_POZZETO"
	DEACTIVATE FOLDER
	
	//Rotate Centrino to Workplane
	ACTIVATE Workplane $WP_POZZETO
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_LEFT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_TOP"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_RIGHT"
	EDIT FEATURESET ; FEATURE SELECTED AXIS K "1"
	EDIT FEATURESET ; FEATURE SELECTED APPLYAXIS
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" DESELECT "CENTERING_LEFT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" DESELECT "CENTERING_TOP"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" DESELECT "CENTERING_RIGHT"
	
	//Centering hole left
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_LEFT"
	EDIT FEATURESET ; FEATURE SELECTED POSITION X "-$DIAMETER/2 - 6"
	EDIT FEATURESET ; FEATURE SELECTED POSITION Z "$HEIGHT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" DESELECT "CENTERING_LEFT"
	//Centering hole top
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_TOP"
	EDIT FEATURESET ; FEATURE SELECTED POSITION Y "$DIAMETER/2 + 6"
	EDIT FEATURESET ; FEATURE SELECTED POSITION Z "$HEIGHT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" DESELECT "CENTERING_TOP"
	//Centering hole right
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_RIGHT"
	EDIT FEATURESET ; FEATURE SELECTED POSITION X "$DIAMETER/2 + 6"
	EDIT FEATURESET ; FEATURE SELECTED POSITION Z "$HEIGHT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" DESELECT "CENTERING_RIGHT"
	
	CALL CentrinoToolpathPozzeto($WP_POZZETO)
	
}

//Female Pozzeto Toolpath Creation
FUNCTION CentrinoToolpathPozzeto(STRING $WP_POZZETO) {
	
	//Toolpaths Variables
	//CENTERING_HOLES           Toolpath                Index       Tool    Depth
	//                              10        20        30        40        50
	//                      012345678901234567890123456789012345678901234567890
	STRING $PACKET       = "CENTERING HOLES POZZETO       1         T203      8"
	STRING $PATHNAME = RTRIM(SUBSTRING($PACKET , 0 , 29))
	INT $INDEX = SUBSTRING($PACKET , 30 , 1)
	STRING $TOOLNUMBER = SUBSTRING($PACKET , 40 , 4)
	REAL $DRILLDEPTH = SUBSTRING($PACKET , 50 , 3)
	STRING $NAME = ""
	REAL $SPEED = 0
	REAL $FEED = 0
	REAL $PLUNGE = 0
	EDIT TOOLPATH THICKNESS LIST UPDATE 0 NEW	
	
	//Import Toolpath
	IF ENTITY_EXISTS('Toolpath' , $PATHNAME) {
		DELETE Toolpath $PATHNAME NOQUIBBLE
	}
	IMPORT TEMPLATE PROJECT FILEOPEN "V:\Master Files\Alumat\Powermill\Tamplates\Pozzeto\Centrino\Centrino.ptf"
		
	//Drill Toolpath
	ACTIVATE Toolpath $PATHNAME
	//Workplane
	ACTIVATE WORKPLANE $WP_POZZETO
	//Depth
	EDIT DRILL DEPTH $DRILLDEPTH
	//Tool
	CALL $AlumatToolsForDrilling($TOOLNUMBER , $NAME , $SPEED , $FEED , $PLUNGE)
	ACTIVATE TOOL $NAME
	EDIT RPM $SPEED
	EDIT FRATE $FEED
	EDIT PRATE $PLUNGE
	//Rapids
	BLANK UNDO
	EDIT TOOLPATH SAFEAREA CALCULATE_DIMENSIONS
	//Component Thickness
	BLANK UNDO
	EDIT LEVEL "264" SELECT ALL
	EDIT TOOLPATH THICKNESS LIST UPDATE 0 NEW
	EDIT TOOLPATH ; THICKNESS ACQUIRE
	EDIT TOOLPATH ; THICKNESS COMPONENTS IGNORE
	//Select Featureset Components
	DIALOGS MESSAGE OFF
	DIALOGS ERROR OFF
	ACTIVATE Featureset "CENTERING_HOLES_POZZETO"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_LEFT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_LEFT_IN"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_LEFT_OUT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_TOP"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_TOP_IN"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_TOP_OUT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_RIGHT"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_RIGHT_IN"
	EDIT FEATURESET "CENTERING_HOLES_POZZETO" SELECT "CENTERING_RIGHT_OUT"
	DIALOGS MESSAGE ON
	DIALOGS ERROR ON
	//Calculate
	EDIT TOOLPATH $PATHNAME CALCULATE
	EDIT FOLDER "Toolpath\NOCCIOLO" ORDER "Toolpath\NOCCIOLO\CENTERING HOLES POZZETO" FIRST
	BLANK UNDO
	DEACTIVATE Toolpath
	DEACTIVATE Tool
	DEACTIVATE Featureset
	
}

INCLUDE "V:\Master Files\Alumat\Powermill\Tools\ToolsForDrilling.inc"
